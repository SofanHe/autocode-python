unit Json2Flowchart;

interface

//{$DEFINE  _DEBUG}

uses
     //自编模块
     SysRecords,
     SysConsts,
     SysUnits,
     SysVars,
     //
     teUnit,

     //第三方控件
     GDIPAPI,GDIPOBJ,

     //
     JsonDataObjects,


     //系统自带
     //XMLDoc,XMLIntf,
     Vcl.ComCtrls,
     Forms,Math,Graphics,SysUtils,Dialogs,windows,Classes,ExtCtrls;


//==============================绘制流程图函数====================================================//
//VST : 当前VirtualStringTree
//Node : 当前节点
//Image : 输出的图片
//Config : 配置信息
function  JsonToFlowChart(ANode:TJsonObject;Image:TImage;Config:TWWConfig):Integer;

procedure SetNodeStatus(ANode:TTreeNode);


implementation

procedure SetNodeStatus(ANode:TTreeNode);
var
     iItem     : Integer;
     joNode    : TJsonObject;
begin
     try
          //
          joNode    := teTreeToJson(ANode);

          joNode.B['Expanded']     := ANode.Expanded;
          joNode.Remove('X');
          joNode.Remove('Y');
          joNode.Remove('E');
          joNode.Remove('W');
          joNode.Remove('H');
          for iItem := 0 to Min(ANode.Count-1,joNode.A['items'].Count-1) do begin
               SetNodeStatus(ANode.Item[iItem]);
          end;
     except
          ShowMessageFmt('Error when SetNodeStatus! tnNode = %s, xnNode = %s',[ANode.Text]);
     end;
end;



//设置当前节点的所有子节点的X,Y,W,H,E为-1;
procedure SetChildNull(Node:TJsonObject);
var
     I         : Integer;
begin
     Node.I['X']     := -1;
     Node.I['Y']     := -1;
     Node.I['W']     := -1;
     Node.I['H']     := -1;
     Node.I['E']     := -1;
     for I:=0 to Node.A['items'].Count-1 do begin
          SetChildNull(Node.A['items'][I]);
     end;
end;

function  JsonToFlowChart(ANode:TJsonObject;Image:TImage;Config:TWWConfig):Integer;
type
     TNodeWHE = record
          W,H,E     : Integer;
     end;
var
     I,J,K     : Integer;
     iLevel    : Integer;
     iRight    : Single;      //用于记录Case中上一子块的右边界值
     joNode    : TJsonObject;    //临时节点
     joChild   : TJsonObject;    //子节点
     joExtra   : TJsonObject;    //额外子节点
     joLast    : TJsonObject;    //当前最后的XML节点
     joNext    : TJsonObject;
     
     iMaxLevel : Integer;     //最深层次
     iMinLevel : Integer;     //最浅层次
     oCur      : TJsonObject;
     oFor      : TJsonObject;     //用于判断FOR的E的块
     oPar      : TJsonObject;
     oChild    : TJsonObject;
     oExtra    : TJsonObject;
     sText     : string;      //用于保存模块的TEXT
     //
     BW,BH     : Integer;
     SH,SV     : Integer;
     iMaxE     : Integer;
     iMaxH     : Integer;
     iPos      : Integer;
     X,Y,W,H,E : Integer;
     iImageW   : Integer;     //流程图图片宽度
     iImageH   : Integer;     //流程图图片高度
     //
     iTop      : Integer;
     sInit     : String;
     sJudge    : String;
     sNext     : String;

     //
     iTmp      : Single;
     bTmp      : Boolean;
     sTmp      : String;

     //
     iCode     : Integer;
     slTmp     : TStringList;

     //
     oPen      : TGPPen;
     oGraph    : TGPGraphics;
     oBrush    : TGPSolidBrush;
     oFontBrh  : TGPSolidBrush;
     oFont     : TGPFont;
     oFontB    : TGPFont;
     oFormat   : TGPStringFormat;
     oPath     : TGPGraphicsPath;

     //
     fCurScale : Double;      //当前辅助缩放参数
{$IFDEF _DEBUG}
     xDebug    : TXMLDocument;
     xNode     : TJsonObject;
{$ENDIF}

const
     iDeltaX = 2;
     iDeltaY = 7;

     //
     procedure DrawPoints(Pts:array of Single);overload;
     var
          I,iCount  : Integer;
          rPath     : TGPGraphicsPath;
          rGPPts    : array[0..99] of TGPPointF;
     begin

          //得到点数
          iCount    := Length(Pts) div 2;
          //设置点坐标
          for I:=0 to iCount-1 do begin
               rGPPts[I].X    := Pts[I*2];
               rGPPts[I].Y    := Pts[I*2+1];
          end;
          //生成路径
          rPath     := TGPGraphicsPath.Create;
          rPath.AddLines(PGPPointF(@rGPPts[0]),iCount);
          //绘制
          oGraph.DrawPath(oPen,rPath);

          //释放退出
          rPath.destroy;

          //---------------
          Image.Canvas.MoveTo(Round(Pts[0]),Round(Pts[1]));
          //得到点数
          iCount    := Length(Pts) div 2;
          //设置点坐标
          for I:=1 to iCount-1 do begin
               Image.Canvas.LineTo(Round(Pts[I*2]),Round(Pts[I*2+1]));
          end;


     end;

     //输入各点坐标,绘制折线
     procedure DrawPoints(Pts:array of Single;Color:TColor);overload;
     var
          I,iCount  : Integer;
          rPath     : TGPGraphicsPath;
          rGPPts    : array[0..99] of TGPPointF;
          rRegion   : TGPRegion;
          oIFBrush  : TGPSolidBrush;
     begin

          //得到点数
          iCount    := Length(Pts) div 2;
          //设置点坐标
          for I:=0 to iCount-1 do begin
               rGPPts[I].X    := Pts[I*2];
               rGPPts[I].Y    := Pts[I*2+1];
          end;
          //生成路径
          rPath     := TGPGraphicsPath.Create;
          rPath.AddLines(PGPPointF(@rGPPts[0]),iCount);

          //<如果需要涂色,即涂色
          // Construct a region based on the path.
          rRegion   := TGPRegion.Create(rPath);
          //
          oIFBrush  := TGPSolidBrush.Create(ColorToGP(Color));
          //
          oGraph.FillRegion(oIFBrush,rRegion);
          //
          rRegion.Destroy;
          oIFBrush.Destroy;
          //>

          //绘制
          oGraph.DrawPath(oPen,rPath);

          //释放退出
          rPath.destroy;

     end; //end of DrawPoints

     //绘制向上或向上箭头(iX,iY为中心点坐标)
     procedure DrawArrow(iX,iY:Single;bDown:Boolean);
     begin
          if bDown then begin
               iY   := iY+iDeltaY*Config.Scale/2;
               DrawPoints([iX,iY,  iX+iDeltaX*Config.Scale,iY-iDeltaY*Config.Scale,
                         iX-iDeltaX*Config.Scale,iY-iDeltaY*Config.Scale,  iX,iY]);
          end else begin
               iY   := iY-iDeltaY*Config.Scale/2;
               DrawPoints([iX,iY,  iX+iDeltaX*Config.Scale,iY+iDeltaY*Config.Scale,
                         iX-iDeltaX*Config.Scale,iY+iDeltaY*Config.Scale,  iX,iY]);
          end;
     end; //end of DrawArrow

     //绘制菱形框,iX,iY为上顶点坐标
     procedure DrawDiamond(iX,iY:Single;Text:String);
     var
          rGPPts    : array[0..3] of TGPPointF;
          rPath     : TGPGraphicsPath;
          rRegion   : TGPRegion;
          oIFBrush  : TGPSolidBrush;
     begin
          rGPPts[0].X    := iX;
          rGPPts[0].Y    := iY;
          rGPPts[1].X    := iX-BW;
          rGPPts[1].Y    := iY+BH;
          rGPPts[2].X    := iX;
          rGPPts[2].Y    := iY+BH*2;
          rGPPts[3].X    := iX+BW;
          rGPPts[3].Y    := iY+BH;
          //
          rPath     := TGPGraphicsPath.Create;
          rpath.AddPolygon(PGPPointF(@rGPPts), 4);

          // Construct a region based on the path.
          rRegion   := TGPRegion.Create(rPath);
          //
          oIFBrush  := TGPSolidBrush.Create(ColorToGP(Config.IFColor));
          //绘制菱形
          oGraph.FillRegion(oIFBrush,rRegion);
          //绘制外框
          oPen.SetColor(ColorToGP(Config.IFColor));
          oGraph.DrawPath(oPen,rPath);
          oPen.SetColor(ColorToGP(Config.LineColor));
          //写文字
          oGraph.DrawString(Text,-1,oFontB,MakeRect(iX-BW,iY,BW*2,BH*2.0),oFormat,oFontBrh);
          DrawPoints([iX,iY,  iX-BW,iY+BH,  iX,iY+BH*2,  iX+BW,iY+BH,  iX,iY]);

          //
          oIFBrush.Free;
          rRegion.Free;
          rPath.Free;
     end;

     //绘制一般方框(iX,iY为上边中心点坐标，Text为文本,Collaped为是否合拢块标志)
     procedure DrawBlock(iX,iY:Single;Text:String;Collapsed:Boolean);
     begin
          DrawPoints([iX-BW,iY,  iX+BW,iY,  iX+BW,iY+BH,  iX-BW,iY+BH,  iX-BW,iY]);
          oGraph.DrawString(Text,-1,oFont,MakeRect(iX*1.0-BW,iY,BW*2,BH),oFormat,oFontBrh);
          if Collapsed then begin
               //两侧的竖线
               DrawPoints([iX-BW+5,iY,  iX-BW+5,iY+BH]);
               DrawPoints([iX+BW-5,iY,  iX+BW-5,iY+BH]);
          end;
     end;

     //绘制代码方框(iX,iY为上边连接点坐标，iW,iH为长和宽,Text为文本)
     procedure DrawCodeBlock(iX,iY,iW,iH:Single;Text:String);
     begin
          oFormat.SetAlignment(StringAlignmentNear);
          DrawPoints([iX-BW,iY,  iX-BW+iW,iY,  iX-BW+iW,iY+iH,  iX-BW,iY+iH,  iX-BW,iY]);
          oGraph.DrawString(Text,-1,oFont,MakeRect(X*1.0-BW+4,Y,iW-4,iH),oFormat,oFontBrh);    //4用于代码与左边有缩进,比较美观
          oFormat.SetAlignment(StringAlignmentCenter);
     end;

     //绘制起始标志,双半圆弧+方框(iX,iY为上边中心点坐标，Text为文本)
     procedure DrawRoundRect(iX,iY:Single;Text:String);
     begin
          iTmp := Round(BW/2);    //半宽,宽度的一半
          oPath.CloseAllFigures;
          oPath.AddLine(X-iTmp+1,Y,  X+iTmp-1,Y);
          oPath.AddArc(X+iTmp-BH/2,Y,BH,BH,-90,180);
          oPath.AddLine(X-iTmp+1,Y+BH,  X+iTmp-1,Y+BH);
          oPath.AddArc(X-iTmp-BH/2,Y,BH,BH,90,180);
          oGraph.DrawPath(oPen,oPath);
          oGraph.DrawString(Text,-1,oFontB,MakeRect(X-BW,Y,BW*2.0,BH),oFormat,oFontBrh);
     end;


     //绘制TRY的各种形状 (iX,iY为上边中心点坐标，Text为文本,Mode为类型,0:TRY,1:EXCEPT/FINALLY,3.END)
     procedure DrawTry(iX,iY:Single;Text:String;Collapsed:Boolean;Mode:Integer);
     begin
          case Mode of
               //Try
               0 : begin
                    DrawPoints([X,Y+BH,  X-BW,Y+BH,  X-BW,Y,  X+BW,Y,  X+BW-BH,Y+BH,  X,Y+BH,  X,Y+BH+SV]);
                    oGraph.DrawString(Text,-1,oFontB,MakeRect(X*1.0-BW,Y,BW*2,BH),oFormat,oFontBrh);
               end;

               //except/finally
               1 : begin
                    //绘制
                    DrawPoints([X,Y+BH,  X-BW,Y+BH,  X-BW,Y,  X+BW,Y,  X+BW-BH*1/2,Y+BH/2,  X+BW,Y+BH,  X,Y+BH,  X,Y+BH+SV]);
                    oGraph.DrawString(Text,-1,oFontB,MakeRect(X*1.0-BW,Y,BW*2,BH),oFormat,oFontBrh);
                    if Collapsed then begin
                         //左侧的竖线
                         DrawPoints([X-BW+5,Y,  X-BW+5,Y+BH]);
                    end;
               end;

               //end of try
               2 : begin
                    //绘制End of Try
                    iTmp := Y+H-BH-SV;
                    DrawPoints([X,iTmp+BH,  X-BW,iTmp+BH,  X-BW,iTmp,  X+BW-BH,iTmp,  X+BW,iTmp+BH,  X,iTmp+BH,  X,iTmp+BH+SV]);
                    oGraph.DrawString(Text,-1,oFontB,MakeRect(X*1.0-BW,iTmp,BW*2,BH),oFormat,oFontBrh);
               end;
          end;
     end;
     
     procedure DrawText(Text:String;X,Y,W,H:Single);
     begin
          oGraph.DrawString(Text,-1,oFontB,MakeRect(X,Y,W,H),oFormat,oFontBrh);
     end;
     procedure GetGPTextWH(Text:string;
               Font:TGPFont;FontFormat:TGPStringFormat;
               FontFamily:TGPFontFamily;
               var Width:Single;var Height:Single);
     var
          oPath   : TGPGraphicsPath;
          oRect   : TGPPointF;
          strFormat    : TGPStringFormat;
          rcBound      : TGPRectF;
     begin
          oPath     := TGPGraphicsPath.Create;
          strFormat := TGPStringFormat.Create();
          oPath.AddString(Text,-1,
                    FontFamily,
                    font.GetStyle(),
                    font.GetSize(),
                    oRect,strFormat);
          oPath.GetBounds(rcBound);

          //
          Width     := rcBound.Width;
          Height    := rcBound.Height;
     end;
     function GetGPTextWidth(Text:string):Single;
     var
          W,H       : Single;
          oFF       : TGPFontFamily;
          oFormat   : TGPStringFormat;
     begin
          oFormat   := TGPStringFormat.Create();
          oFF       := TGPFontFamily.Create;
          oFont.GetFamily(oFF);//oFF       := TGPFontFamily.Create(Config.FontName);

          GetGPTextWH(Text,oFont,oFormat,oFF,W,H);
          Result    := W;
          oFormat.Destroy;
          oFF.Destroy;
     end;
     function GetGPTextHeight(Text:string):Single;
     var
          W,H       : Single;
          oFF       : TGPFontFamily;
          oFormat   : TGPStringFormat;
     begin
          oFormat   := TGPStringFormat.Create();
          oFF       := TGPFontFamily.Create;
          oFont.GetFamily(oFF);//oFF       := TGPFontFamily.Create(Config.FontName);
          GetGPTextWH(Text,oFont,oFormat,oFF,W,H);
          Result    := H;
          oFormat.Destroy;
          oFF.Destroy;
     end;

     procedure SetNodePosition(Node:TJsonObject);
     var
          II,JJ     : Integer;
     begin
          //如果该节点合拢,则不必要分析其子节点的位置
          if not Node.B['Expanded'] then begin
               Exit;
          end;
          
          //<根据当前节点的类型计算子节点位置
          if Node.s['name'] = 'if' then begin
               //YES块
               joChild   := Node.A['items'][0];
               joChild.I['X']  := Node.I['X'];
               joChild.I['Y']  := Node.I['Y']+BH*2+SV;

               //ELIF / NO块
               for JJ := 1 to Node.A['items'].Count-1 do begin
                    joExtra   := Node.A['items'][JJ];
                    joExtra.I['X']  := joChild.I['X']- + SH + _E(joExtra) + BW;
                    joExtra.I['Y']  := _Y(joChild);
                    //
                    joChild   := joExtra;
               end;
          end else if Node.s['name'] = 'for' then begin
               //子块
               joChild   := Node.A['items'][0];
               joChild.I['X']  := Node.I['X'];
               joChild.I['Y']  := Node.I['Y']+ BH+Sv;

          end else if Node.s['name'] = 'while' then begin
               //子块
               joChild   := Node.A['items'][0];
               joChild.I['X']  := Node.I['X'];
               joChild.I['Y']  := Node.I['Y']+ BH*2+Sv*2;
          end else if Node.s['name'] = 'try' then begin
               iTop := 0;
               for JJ:=0 to Node.A['items'].Count-1 do begin
                    if JJ=0 then begin
                         joChild   := Node.A['items'][0];
                         //
                         joChild.I['X']  := Node.I['X'];
                         joChild.I['Y']  := Node.I['Y']+BH+SV;
                         iTop := joChild.I['Y']+joChild.I['H'];
                    end else begin
                         joChild   := Node.A['items'][JJ];
                         //
                         joChild.I['X']  := Node.I['X'];
                         joChild.I['Y']  := iTop;
                         //
                         iTop := joChild.I['Y']+joChild.I['H'];
                    end;
               end;
          end else if (Node.s['name'] = 'try_except') or (Node.s['name'] = 'try_else') then begin

               iTop := 0;
               for JJ:=0 to Node.A['items'].Count-1 do begin
                    if JJ=0 then begin
                         joChild   := Node.A['items'][0];
                         //
                         joChild.I['X']  := Node.I['X'];
                         joChild.I['Y']  := Node.I['Y']+BH+SV;
                         iTop := joChild.I['Y']+joChild.I['H'];
                    end else begin
                         joChild   := Node.A['items'][JJ];
                         //
                         joChild.I['X']  := Node.I['X'];
                         joChild.I['Y']  := iTop;
                         //
                         iTop := joChild.I['Y']+joChild.I['H'];
                    end;
               end;
          end else begin
               iTop := 0;
               for JJ:=0 to Node.A['items'].Count-1 do begin
                    joChild   := Node.A['items'][JJ];
                    //
                    joChild.I['X']  := Node.I['X'];
                    joChild.I['Y']  := Node.I['Y']+iTop;
                    //
                    iTop := iTop+joChild.I['H'];

               end;
               iRight    := 0;

          end;


          //递归计算子节点的子节点位置
          for II:=0 to Node.A['items'].Count-1 do begin
               SetNodePosition(Node.A['items'][II]);
          end;
     end;
     //
     procedure DrawNodeFlowchart(Node:TJsonObject);
     var
          II,JJ     : Integer;
     begin
          try
               //赋给简单变量以便于书写
               X    := Node.I['X'];
               Y    := Node.I['Y'];
               E    := Node.I['E'];
               W    := Node.I['W'];
               H    := Node.I['H'];

               //
               if Node.I['W']=-1 then begin
                    Exit;
               end;

               //<绘制子节点数为0的节点和合拢的节点
               if (Node.A['items'].Count=0) then begin
                    //处理无子块节点(不包括跳转及分支)
                    if (Node.S['name']='code') and gbShowDetailCode  then begin
                         //节点(矩形)
                         DrawCodeBlock(X,Y,W,H-SV,Node.S['Text']);
                         //下接线
                         DrawPoints([X,Y+H-SV,  X,Y+H]);
                         //
                         Exit;
                    end else if not teInNames(Node.S['name'],['try_except','try_else']) then begin
                         if teInNames(Node.S['name'],['if_else','elif']) then begin

                              //下接线
                              DrawPoints([X,Y,  X,Y+BH+SV]);
                              //
                              Exit;
                         end else begin
                              //节点(矩形)
                              DrawBlock(X,Y,GetNodeText(Node),False);
                              //下接线
                              DrawPoints([X,Y+BH,  X,Y+BH+SV]);
                              //
                              Exit;
                         end;
                    end;
               end else if (Node.B['Expanded']=False) then begin
                    //处理合拢的节点(不包括分支)
                    if not teInNames(Node.S['name'],['try_except','try_else']) then begin
                         //合拢节点(矩形)
                         DrawBlock(X,Y,GetNodeText(Node),False);  //RTtoStr(Node.Attributes['Mode'])
                         //下接线
                         DrawPoints([X,Y+BH,  X,Y+BH+SV]);
                         //
                         Exit;
                    end;
               end;
               //>


               //
               if Node.S['name'] = 'if' then begin
                    //菱形框
                    DrawDiamond(X,Y,Format('%s',[GetNodeText(Node)]));
                    DrawPoints([X,Y+BH*2,  X,Y+BH*2+SV]); //向下线
                    //菱形框向右延伸线
                    joChild   := Node.A['items'][0];
                    if Node.A['items'][1].S['name'] = 'elif' then begin
                         //如果第1个子块为elif,则到左边距EL
                         DrawPoints([joChild.I['X']+BW,Y+BH,  _EL(Node.A['items'][1]),Y+BH]);
                    end else begin
                         //如果第1个子块不是elif,则该块的中轴线,即X
                         DrawPoints([joChild.I['X']+BW,Y+BH,  Node.A['items'][1].I['X'],Y+BH]);
                    end;

                    //
                    for JJ:=1 to Node.A['items'].Count-1 do begin
                         joChild   := Node.A['items'][JJ];
                         if joChild.S['name'] = 'elif' then begin
                              //菱形框
                              DrawDiamond(joChild.I['X'],_Y(joChild)-BH*2-SV,GetNodeText(joChild));
                              //菱形框向下线
                              DrawPoints([joChild.I['X'],_Y(joChild)-SV,_X(joChild),_Y(joChild)]);
                              //菱形框向右延伸线
                              DrawPoints([joChild.I['X']+BW,_Y(joChild)-SV-BH,_EL(Node.A['items'][JJ+1]),_Y(joChild)-SV-BH]);

                         end else begin
                              //接上模块菱形框向右延伸线
                              DrawPoints([_L(joChild),_Y(joChild)-SV-BH,_X(joChild),_Y(joChild)-SV-BH]);
                              //无菱形框向下线
                              DrawPoints([_X(joChild),_Y(joChild)-SV-BH,_X(joChild),_Y(joChild)]);
                         end;
                         DrawPoints([_X(joChild),_B(joChild),_X(joChild),_EB(Node)]); //模块下面的下接线
                    end;

                    //横向多模块下接线
                    DrawPoints([X,Y+H-SV,_X(Node.A['items'].O[Node.A['items'].Count-1]),Y+H-SV]);
                    //YES块的下接线
                    DrawPoints([X,_B(Node.A['items'][0]),  X,Y+H]);

               end else if Node.S['name'] = 'for' then begin
                    //FOR框
                    DrawPoints([X-BW,Y,  X+W-BW-Sh-BH,Y,  X+W-BW-Sh,Y+BH/2,  X+W-BW-Sh-BH,Y+BH,  X-BW,Y+BH,  X-BW,Y],Config.IFColor);
                    DrawText(''+GetNodeText(Node),X-BW,Y,W-Sh-BH/2,BH);
                    DrawPoints([X,Y+BH,  X,Y+BH+SV]);
                    //得到子块
                    joChild   := Node.A['items'][0];
                    //退出循环线
                    DrawPoints([X+W-BW-Sh,Y+BH/2,  X+W-BW,Y+BH/2,  X+W-BW,Y+H-SV,  X,Y+H-SV,  X,Y+H]);
                    DrawArrow(X+W-BW,Y+H / 2, True);
                    //继续循环线
                    DrawPoints([X,Y+H-SV*3,  X,Y+H-SV*2,  X-BW-E,Y+H-SV*2,  X-BW-E,Y+BH/2,  X-BW,Y+BH/2]);
                    DrawArrow(X-BW-E,Y+H / 2, False);

               end else if Node.S['name'] = 'while' then begin
                    //菱形框
                    DrawDiamond(X,Y+SV,Format('%s',[GetNodeText(Node)]));
                    DrawPoints([X,Y+BH*2+SV,  X,Y+BH*2+SV*2]);
                    //得到子块
                    joChild   := Node.A['items'][0];
                    //退出循环线
                    DrawPoints([X+BW,Y+BH+SV,  X+W-BW,Y+BH+SV,  X+W-BW,Y+H-SV,  X,Y+H-SV,  X,Y+H]);
                    DrawArrow(X+W-BW,Y+H / 2, True);
                    //继续循环线
                    DrawPoints([X,joChild.I['Y']+joChild.I['H'],
                              X,Y+H-SV*2,  X-BW-E,Y+H-SV*2,  X-BW-E,Y,  X,Y,  X,Y+SV]);
                    DrawArrow(X-BW-E,Y+H / 2, False);

               end else if Node.S['name'] = 'elif' then begin
                    //如果当前子块未展开,则绘制一个
                    if (Node.B['Expanded']=False) then begin
                         if Node.Contains('items') then begin
                              iTmp := Y;
                              DrawBlock(x,iTmp,'... ...',True);
                              //下接线
                              DrawPoints([X,iTmp+BH,  X,iTmp+BH+SV]);

                         end;
                    end ;

               end else if Node.S['name'] = 'try' then begin
                    //绘制Try
                    DrawTry(X,Y,'try',True,0);

                    //绘制End of Try
                    //iTmp := Y+H-BH-SV;
                    //DrawTry(X,iTmp,'TRY END',True,2);

               end else if Node.S['name'] = 'try_except' then begin
                    //绘制
                    DrawTry(X,Y,Node.S['source'],not Node.B['Expanded'],1);
               end else if Node.S['name'] = 'try_else' then begin
                    //绘制
                    DrawTry(X,Y,'else',not Node.B['Expanded'],1);
               end;


               //递归绘制其子节点
               if Node.B['Expanded'] then begin
                    for II:=0 to Node.A['items'].Count-1 do begin
                         DrawNodeFlowchart(Node.A['items'][II]);
                    end;
               end;
          except
               ShowMessage('Error when DrawNodeFlowchart! '+Node.S['name']);
          end;

     end;
     procedure ClearNodeWHE(Node:TJsonObject);
     var
          II   : Integer;
     begin
          //Node.AttributeNodes.Delete('W');
          //Node.AttributeNodes.Delete('H');
          //Node.AttributeNodes.Delete('E');
          for II:=0 to Node.A['items'].Count-1 do begin
               ClearNodeWHE(Node.A['items'][II]);
          end;
     end;


     function GetNodeWHE(Node:TJsonObject):TNodeWHE;
     var
          iiCode    : Integer;
          KK        : Integer;
          joFirst   : TJsonObject;
          joNext    : TJsonObject;
          rChild    : TNodeWHE;
          rExtra    : TNodeWHE;
     begin
          //如果已计算过,则直接出结果
          if Node.Contains('W') then begin
               Result.W  := Node.I['W'];
               Result.H  := Node.I['H'];
               Result.E  := Node.I['E'];
               //
               Exit;
          end;
          //
          Result.W  := -1;
          Result.H  := -1;
          Result.E  := -1;
          if Node.S['name'] = 'code' then begin
               //处理代码
               if gbShowDetailCode=False then begin
                    Node.I['W']   := BW*2;
                    Node.I['H']   := BH+Sv;
                    Node.I['E']   := 0;
               end else begin
                    //<处理代码
                    slTmp     := TStringList.Create;
                    slTmp.Text     := GetNodeText(Node);
                    Node.I['W']   := BW*2;
                    for iiCode:=0 to slTmp.Count-1 do begin
                         slTmp[iiCode]   := Trim(slTmp[iiCode]);
                         Node.I['W']   := Max(Node.I['W'],Round(GetGPTextWidth(slTmp[iiCode]+'A   A')));
                    end;
                    //删除最后一行空行
                    if slTmp.Count>0 then begin
                         if slTmp[slTmp.Count-1]='' then begin
                              slTmp.Delete(slTmp.Count-1);
                         end;
                    end;
                    //保存到变量
                    sText     := slTmp.Text;
                    iiCode     := slTmp.Count;
                    Node.S['Text']  := slTmp.Text;
                    //
                    slTmp.Destroy;
                    //>


                    //计算长宽
                    Node.I['H']   := Max(BH,Round(GetGPTextHeight(Node.S['Text']+#13+'AA'+#13+'AA')))+Sv;
                    Node.I['E']   := 0;
                    //取整
                    Node.I['W']     := Round(Node.I['W']);
                    Node.I['H']     := Round(Node.I['H']);
               end;
               //
               Result.W  := Node.I['W'];
               Result.H  := Node.I['H'];
               Result.E  := Node.I['E'];
          end else if (Node.Contains('items')=False)and (not teInNames(Node.S['name'],['try_except','try_else'])) then begin
               //处理简单模块
               Node.I['W']   := BW*2;
               Node.I['H']   := BH+Sv;
               Node.I['E']   := 0;
               //
               Result.W  := Node.I['W'];
               Result.H  := Node.I['H'];
               Result.E  := Node.I['E'];
          end else if (not (Node.B['Expanded'])) then begin
               Node.I['W']   := BW*2;
               Node.I['H']   := BH+sv;
               Node.I['E']   := 0;
               //
               Result.W  := Node.I['W'];
               Result.H  := Node.I['H'];
               Result.E  := Node.I['E'];
          end else begin
               if Node.A['items'].Count>0 then begin
                    joFirst   := Node.A['items'][0];
                    rChild    := GetNodeWHE(joFirst);
               end;

               //指定默认的WHE
               Node.I['W']   := BW*2;
               Node.I['H']   := BH+sv;
               Node.I['E']   := 0;

               //根据父块类型得到父块的W,H,E
               if Node.S['name'] = 'if' then begin
                    //对多分支语句是否展开分开进行处理
                    for KK:=0 to Node.A['items'].Count-1 do begin
                         if KK = 0 then begin
                              joFirst   := Node.A['items'][0];
                              rChild    := GetNodeWHE(joFirst);
                              Node.I['E']   := rChild.E;
                              Node.I['W']   := rChild.W;
                              Node.I['H']   := BH*2+SV*2+rChild.H;
                         end else begin
                              joFirst   := Node.A['items'][KK];
                              rChild    := GetNodeWHE(joFirst);
                              //
                              Node.I['W']   := Node.I['W']+SH+rChild.E+rChild.W;
                              Node.I['H']   := Max(Node.I['H'],rChild.H+BH*2+SV*2);
                         end;
                    end;

               end else if Node.S['name'] = 'for' then begin
                    Node.I['W']   := rChild.W + Sh;
                    Node.I['H']   := rChild.H + BH+Sv*3;
                    Node.I['E']   := rChild.E + Sh;
                    //>
               end else if Node.S['name'] = 'while' then begin
                    Node.I['W']   := rChild.W + Sh;
                    Node.I['H']   := rChild.H + BH*2+Sv*4;
                    Node.I['E']   := rChild.E + Sh;

               end else if Node.S['name'] = 'try' then begin
                    for KK:=0 to Node.A['items'].Count-1 do begin
                         if KK=0 then begin
                              joFirst   := Node.A['items'][0];
                              rChild    := GetNodeWHE(joFirst);
                              Node.I['E']   := rChild.E;
                              Node.I['W']   := rChild.W;
                              Node.I['H']   := rChild.H+BH*2+SV*2;
                         end else begin
                              joFirst   := Node.A['items'][KK];
                              rChild    := GetNodeWHE(joFirst);
                              //
                              Node.I['E']   := Max(Node.I['E'],rChild.E);
                              Node.I['W']   := Max(Node.I['W'],rChild.W);
                              Node.I['H']   := Node.I['H']+rChild.H;
                         end;
                    end;

               end else if (Node.S['name'] = 'try_except') or (Node.S['name'] = 'try_else') then begin
                    if Node.B['Expanded'] then begin
                         for KK:=0 to Node.A['items'].Count-1 do begin
                              if KK=0 then begin
                                   joFirst   := Node.A['items'][0];
                                   rChild    := GetNodeWHE(joFirst);
                                   Node.I['E']   := rChild.E;
                                   Node.I['W']   := rChild.W;
                                   Node.I['H']   := rChild.H+BH+SV;
                              end else begin
                                   joFirst   := Node.A['items'][KK];
                                   rChild    := GetNodeWHE(joFirst);
                                   //
                                   Node.I['E']   := Max(Node.I['E'],rChild.E);
                                   Node.I['W']   := Max(Node.I['W'],rChild.W);
                                   Node.I['H']   := Node.I['H']+rChild.H;
                              end;
                         end;
                    end else begin
                         Node.I['E']   := 0;
                         Node.I['W']   := BW*2;
                         Node.I['H']   := BH+SV;
                    end;
               end else
                    //
                    for KK:=0 to Node.A['items'].Count-1 do begin
                         if KK=0 then begin
                              joFirst   := Node.A['items'][0];
                              rChild    := GetNodeWHE(joFirst);
                              Node.I['E']   := rChild.E;
                              Node.I['W']   := rChild.W;
                              Node.I['H']   := rChild.H;
                         end else begin
                              joFirst   := Node.A['items'][KK];
                              rChild    := GetNodeWHE(joFirst);
                              //
                              Node.I['E']   := Max(Node.I['E'],rChild.E);
                              Node.I['W']   := Max(Node.I['W'],rChild.W);
                              Node.I['H']   := Node.I['H']+rChild.H;
                         end;
                    end;
               end; //end of case
               //
               Result.W  := Node.I['W'];
               Result.H  := Node.I['H'];
               Result.E  := Node.I['E'];
          end;


begin
     Result    := 0;


     //<得到流程图设置
     fCurScale := 1;
     while True do begin
          Config.Scale   := Config.Scale*fCurScale;
          oFont     := TGPFont.Create(Config.FontName, Config.FontSize*Config.Scale, FontStyleRegular, UnitPixel);
          //
          BW   := Round(Config.BaseWidth*Config.Scale);
          BH   := Round(Config.BaseHeight*Config.Scale);
          SH   := Round(Config.SpaceHorz*Config.Scale);
          SV   := Round(Config.SpaceVert*Config.Scale);
          if BW=0 then begin
               BW   := 80;
          end;
          if BH=0 then begin
               BH   := 30;
          end;
          if SH=0 then begin
               SH   := 20;
          end;
          if SV=0 then begin
               SV   := 20;
          end;
          //>

          //
          Image.Canvas.Font.Size   := Round(grConfig.FontSize*Config.Scale);
          Image.Canvas.Font.Name   := grConfig.FontName;

          //-----------------------------------计算各模块的大小-----------------------------------//
          //计算前先清除原来的WHE
          ClearNodeWHE(ANode);
          //用递归来计算各模块的大小
          GetNodeWHE(ANode);

          //-----------------------------------计算各模块的位置-----------------------------------//
          //从第一个开始, 以当前块为父块, 计算当前块各子块的位置
          joNode    := ANode;
          joNode.I['X']   := SH+ANode.I['E']+BW;
          joNode.I['Y']   := BH+SV*2;
          SetNodePosition(joNode);      //递归计算各模块的位置


          //---------------------------绘制流程图的准备工作---------------------------------------//
          //如果当前流程图没有内容,则设置为最小一个块
          if joNode.I['W']<=0 then begin
               joNode.I['W']   := BW*2;
               joNode.I['H']   := BH+Sv;
          end;

          //设置图像大小
          iImageW   := Round(joNode.I['E']+joNode.I['W']+SH*2);
          iImageH   := Round(joNode.I['H']+Sv*2+2*(BH+SV));

          //控制最大宽度和高度
          if (iImageW>giMaxWidth)or(iImageH>giMaxHeight) then begin
               if (iImageW>giMaxWidth)or(iImageH>giMaxHeight) then begin
                    if iImageW/iImageH>giMaxWidth/giMaxHeight then begin
                         fCurScale := giMaxWidth/iImageW;
                         iImageH   := Round(giMaxWidth*(iImageH/iImageW));
                         iImageW   := giMaxWidth;
                    end else begin
                         fCurScale := giMaxHeight/iImageH;
                         iImageW   := Round(giMaxHeight*(iImageW/iImageH));
                         iImageH   := giMaxHeight;
                    end;
               end;
               //
               //Break;
          end else begin
               Break;
          end;
     end;

     //
     Image.Width    := iImageW;
     Image.Height   := iImageH;
     Image.Picture.Bitmap.Width      := iImageW;
     Image.Picture.Bitmap.Height     := iImageH;
     Image.Picture.Assign(nil);

     //生成各种GDI+对象
     oGraph    := TGPGraphics.Create(Image.Canvas.Handle);
     oPen      := TGPPen.Create(ColorToGP(Config.LineColor),1);
     oFont     := TGPFont.Create(Config.FontName, Config.FontSize*Config.Scale, FontStyleRegular, UnitPixel);
     oFontB    := TGPFont.Create(Config.FontName, Config.FontSize*Config.Scale, FontStyleBold, UnitPixel);
     oFontBrh  := TGPSolidBrush.Create(ColorToGP(Config.FontColor));
     oBrush    := TGPSolidBrush.Create(ColorToGP(Config.FillColor));
     oPath     := TGPGraphicsPath.Create();
     oFormat   := TGPStringFormat.Create;
     oFormat.SetAlignment(StringAlignmentCenter);
     oFormat.SetLineAlignment(StringAlignmentCenter);
     //设置反失真
     oGraph.SetSmoothingMode(SmoothingModeAntiAlias);
     oGraph.SetTextRenderingHint(TextRenderingHintAntiAlias);



     //-----------------------绘制流程图(此后的代码应能共享)--------------------------------------//
     //<绘制开始和结束标志
     //起始标志
     X    := ANode.I['X'];
     Y    := SV;
     DrawRoundRect(X,Y,'START');
     //下接线
     DrawPoints([X,Y+BH,  X,Y+BH+SV]);
     //结束标志
     X    := ANode.I['X'];
     Y    := ANode.I['Y']+ANode.I['H'];
     DrawRoundRect(X,Y,'END');
     //>

     //递归绘制流程图
     DrawNodeFlowchart(ANode);

end;


end.
